: ( 41 char-parse drop drop ; immediate
( now we have comments )
: -- 10 char-parse drop drop ; immediate
-- now we have end of line comments

-- data area allocation
: allot ( bytes -- ) ^here + &here ! ;
: cells ( u -- cell * u ) cell * ;
: ,  ( w -- ) ^here ! cell allot ;
: b, ( b -- ) ^here b! 1 allot ;

-- boolean constants
: true ( -- f ) -1 ;
: false ( -- f ) 0 ;

-- abbreviations
: 1+ ( n -- n ) 1 + ;
: 1- ( n -- n) 1 - ;

-- switchs
: on ( addr -- ) true swap ! ;
: off ( addr -- ) false swap ! ;

-- turn immediate mode off in the middle of a definition
: [ ( -- ) immediate-mode on ; immediate
: ] ( -- ) immediate-mode off ;

-- primary flow control words
: ` ( -- xt ) word-parse find-word drop ;
: [`] ( -- ) ` literal ; immediate


-- if else endif statements
: if ( -- source-forward )  [`] branch-false , ^here 0 , ; immediate
: else ( source-forward -- source-forward )
            [`] branch , ^here 0 , swap ^here swap ! ; immediate
: endif ( source-forward -- ) ^here swap ! ; immediate

: ascii ( -- c ) word-parse drop b? ;
: [ascii]   ascii literal ; immediate


: ." [ascii] " char-parse 1- print ;

-- : " [ascii] " char-parse 1-